# qmake feature for advanced source distribution package generation
# Author: Peter Seiderer (for http://www.ciselant.de)
# Date: 3 March 2012
#

COMPRESS = gzip -9f
MAKE = make
TAR = tar -cf

edist.target 	= edist
edist.commands 	= $${LITERAL_DOLLAR}(CHK_DIR_EXISTS) .tmp/$$basename(PWD) || $${LITERAL_DOLLAR}(MKDIR) .tmp/$$basename(PWD); \
		$${LITERAL_DOLLAR}(COPY_FILE) $${_PRO_FILE_} $${DISTFILES} .tmp/$$basename(PWD); \
		for subdir in $${SUBDIRS}; do \
			$${LITERAL_DOLLAR}(MKDIR) .tmp/$$basename(PWD)/$${LITERAL_DOLLAR}$${LITERAL_DOLLAR}{subdir}; \
	  		echo \"Making edist_subdir in $${LITERAL_DOLLAR}$${LITERAL_DOLLAR}{subdir}\"; \
	  		(cd $${LITERAL_DOLLAR}$${LITERAL_DOLLAR}{subdir} && EDIST_SUBDIR=$${LITERAL_DOLLAR}$${LITERAL_DOLLAR}{subdir} $${MAKE} edist_subdir) || exit 1; \
		done; \
        	$${LITERAL_DOLLAR}(MOVE)  .tmp/$$basename(PWD)  .tmp/$$basename(PWD)-$${VERSION}; \
		(cd .tmp && $${TAR} $$basename(PWD)-$${VERSION}.tar $$basename(PWD)-$${VERSION} && $${COMPRESS} $$basename(PWD)-$${VERSION}.tar) && \
		$${LITERAL_DOLLAR}(MOVE) .tmp/$$basename(PWD)-$${VERSION}.tar.gz . && $${LITERAL_DOLLAR}(DEL_FILE) -r .tmp/$$basename(PWD)-$${VERSION}; \
		echo \"Distribution file $$basename(PWD)-$${VERSION}.tar.gz generated.\"

QMAKE_EXTRA_TARGETS += edist

edist_subdir.target = edist_subdir
edist_subdir.commands = $${LITERAL_DOLLAR}(COPY_FILE) $${_PRO_FILE_} $$SOURCES $$HEADERS $$FORMS $${DISTFILES} ../.tmp/$$basename(PWD)/$${LITERAL_DOLLAR}$${LITERAL_DOLLAR}{EDIST_SUBDIR}

QMAKE_EXTRA_TARGETS += edist_subdir

